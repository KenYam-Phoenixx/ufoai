// ==================
// MULTIPLAYER MENU
// ==================

component panel cmpAdr {
{
	size "350 25"
}
	string adr { pos "30 1" size "300 20" tooltip "_Connect to this server"
		onClick { cmd "server_info *cvar:adr<node:parent@num>;" }
		onRClick { cmd "del adr<node:parent@num>; check_bookmarks;" }
	}
	custombutton resetadr { pos "0 1" size "22 22" icon "icons/garbage" tooltip "_Delete this bookmark"
		onClick { cmd "del adr<node:parent@num>; check_bookmarks;" }
		onRClick { cmd "del adr<node:parent@num>; check_bookmarks;" }
	}
}

window multiplayer
{
	confunc mn_push_multiplayer
	{
		cmd "game_setmode multiplayer; ui_push mp_serverbrowser;"
		*cvar:mn_multiplayer_tab = 1
	}

	/**
	 * @param <1> window id
	 * @param <2> name
	 */
	confunc game_addmode_multiplayer
	{
		// TODO: use this to support several multiplayer game types
	}

	/** @brief prevent pushing this menu */
	func onWindowOpened
	{
		cmd "ui_pop;"
		call *node:root.mn_push_multiplayer
	}
}

window abstract_multiplayer
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src		"background/options_bg"
	}

	// ==================
	// main navigation
	// ==================

	cmpMenuButton button_team
	{
		string		"_Team"
		pos			"80 160"
		onClick		{ cmd "ui_pop;ui_push multiplayer_team;" }
	}

	cmpMenuButton button_join
	{
		string		"_Join"
		pos			"80 230"
		onClick		{ cmd "ui_pop; ui_push mp_serverbrowser;" }
	}

	cmpMenuButton button_create
	{
		string		"_Create"
		pos			"80 300"
		onClick		{ cmd "ui_pop; ui_push mp_create_server;" }
	}

	cmpMenuButton button_lobby
	{
		string		"_Lobby"
		pos			"80 385"
		onClick		{ cmd "irc_connect; ui_push irc;" }
	}


	cmpMenuButton button_exit
	{
		string		"_Back"
		pos			"80 470"
		onClick		{ cmd "ui_pop;" }
	}

}

// =====================
// MP TEAM MENU
// =====================
window multiplayer_team extends abstract_multiplayer
{
	cmpTeamPanel content
	{
		{
			pos		"415 100"
			size	"608 768"
		}
	}

	func onWindowOpened
	{
		cmd	"game_teamcomments;define cl_teamdef phalanx;"
	}
}

window abstract_multiplayer_join extends abstract_multiplayer {
	// ==================
	// main navigation
	// ==================

	tab join_navigation {
		{
			pos		"462 73"
			size	"486 40"
			cvar	*cvar:mn_multiplayer_tab
			onChange {
				if ( *cvar:mn_multiplayer_tab == 1 ) {
					cmd "ui_pop;ui_push mp_serverbrowser;"
				} elif ( *cvar:mn_multiplayer_tab == 2 ) {

				} elif ( *cvar:mn_multiplayer_tab == 3 ) {
					cmd "ui_pop;ui_push mp_bookmarks;"
				} elif ( *cvar:mn_multiplayer_tab == 4 ) {
					cmd "irc_connect; ui_push irc;"
				}
			}
		}
		option internet {
			label	"_Internet/Lan"
			value	"1"
		}
/*		option lan {
			label	"_Lan"
			value	"2"
		}
*/
		option bookmark {
			label	"_Bookmark"
			value	"3"
		}
/*
		option bookmark {
			label	"_Lobby"
			value	"4"
		}
*/
	}
}

// ==================
// SERVER INFO
// ==================
window serverinfo
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	image background
	{
		src		"background/multiplayer_bg"
	}

	text server_info
	{
		dataid		TEXT_STANDARD
		pos			"500 200"
		size		"420 490"
		lineheight	20
		tabwidth	250
		rows		"24"
	}

	string user_list_str
	{
		string		"_Userlist"
		pos			"50 115"
		size		"300 20"
		color		"1 1 1 1"
		font		"f_normal_bold"
		textalign	ALIGN_CC
	}
	string user_list_head1
	{
		string		"_Name"
		pos			"50 135"
		size		"150 20"
		color		"1 1 1 1"
		font		"f_verysmall"
		textalign	ALIGN_UL
	}
	string user_list_head2
	{
		string		"_Team"
		pos			"200 135"
		size		"150 20"
		color		"1 1 1 1"
		font		"f_verysmall"
		textalign	ALIGN_UL
	}
	text user_list
	{
		dataid	TEXT_LIST
		pos			"50 170"
		size		"300 380"
		lineheight	20
		tabwidth	200
		font		"f_verysmall"
		mousefx		true
		border		3
		bordercolor	"0 0.5 0 1"
		padding		10
		bgcolor		"0.3 0.3 0.3 0.4"
		rows		"19"
	}

	string mapstring
	{
		string		*cvar:mn_svmapname
		font		"f_menubig"
		color		"1 1 1 1"
		pos			"500 110"
		size		"300 35"
	}

	image mappic
	{
		src		*cvar:mn_mappic
		pos			"800 80"
		texl		"0 0"
		texh		"128 96"
	}

	cmpMenuButton button_connect
	{
		string		"_Connect"
		pos			"70 580"
		onClick		{ cmd "connect *cvar:mn_server_ip;" }
	}

	custombutton button_exit
	{
		tooltip		"_Leave Serverbrowser"
		background	"buttons/large_blue"
		pos			"26 720"
		size		"64 37"
		icon		"icons/return"
		onClick		{ cmd "ui_pop; pingservers;" }
	}

	button button_favorite
	{
		string		"_Bookmark"
		tooltip		"_Add server to bookmarks"
		pos			"150 720"
		size		"178 35"
		image		"ui/button_blue"
		font		"f_normal_bold"
		color		"1 1 1 0.8"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "bookmark_add;" }
	}

	func onWindowOpened
	{
	}

	func onWindowClosed
	{
		cmd	"pingservers; check_bookmarks;"
	}
}

// ==================
// JOIN LAN MENU
// ==================
window mp_serverbrowser extends abstract_multiplayer_join
{
	{
		timeout	60000
	}

	func set_mn_serverliststr_from_id {
		if (<1> == 0) {
			*cvar:mn_serverliststr = "_All"
		} elif (<1> == 1) {
			*cvar:mn_serverliststr = "_Free"
		} elif (<1> == 2) {
			*cvar:mn_serverliststr = "_Playing"
		} else {
			*cvar:mn_serverliststr = "_Invalid"
		}
	}

	panel content {
		{
			pos			"415 120"
			size		"608 768"
		}

		string status_str
		{
			string		"_Serverfilter:"
			pos			"150 7"
			size		"150 20"
		}
		string status_value
		{
			string		*cvar:mn_serverliststr
			pos			"300 7"
			size		"150 20"
			textalign	ALIGN_UC
		}
		custombutton status_dec
		{
			pos			"455 7"
			size		"16 16"
			icon		"icons/prev"
			onClick		{
				*cvar:cl_serverlist = ((*cvar:cl_serverlist - 1) % 2)
				call *node:root.set_mn_serverliststr_from_id( *cvar:cl_serverlist )
				cmd "pingservers refresh;"
			}
		}
		custombutton status_inc
		{
			pos			"470 7"
			size		"16 16"
			icon		"icons/next"
			onClick		{
				*cvar:cl_serverlist = ((*cvar:cl_serverlist + 1) % 2)
				call *node:root.set_mn_serverliststr_from_id( *cvar:cl_serverlist )
				cmd "pingservers refresh;"
			}
		}

		// ====================
		// lan / inet buttons
		// ====================

		// servers_click is CL_ServerListClick_f in cl_main.c
		/* textlist using tabbed text */
		text servers
		{
			pos			"65 40"
			width		"430"
			lineheight	20
			tabwidth	48
			font		"f_verysmall"
			mousefx		true
			border		3
			bordercolor	"0 0.5 0 1"
			padding		10
			bgcolor		"0.3 0.3 0.3 0.4"
			rows		"22"
			dataid		TEXT_LIST
			onClick		{ cmd "servers_click <lineselected>;" }
			onRClick	{ cmd "pingservers;" }
			onViewChange {
				*node:root.content.servers_scroll@fullsize = <fullsize>
				*node:root.content.servers_scroll@current = <viewpos>
				*node:root.content.servers_scroll@viewsize = <viewsize>
			}
		}

		vscrollbar servers_scroll {
			image		"ui/scrollbar_v_green"
			pos			"500 40"
			height		"440"
			current		0
			viewsize	22
			fullsize	22
			onChange	{ *node:parent.servers@viewpos = <current> }
		}

		button button_serverbrowser_refresh
		{
			string		"_Refresh"
			image		"ui/button_green_small"
			color		"0 0.8 0 1"
			font		"f_small"
			pos			"335 490"
			size		"176 32"
			onClick		{ cmd "pingservers refresh;" }
		}

		// ==================
		// nickname
		// ==================
/*
		string current_name
		{
			string		"_Name:"
			pos			"65 455"
			size		"256 20"
		}

		textentry slot_name
		{
			string		*cvar:cl_name
			image		"ui/button_green_verysmall"
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"65 475"
			size		"256 34"
		}
*/

		// ===================
		// connect_to_ip
		// ===================
		string current_inet_connect_to_ip
		{
			string		"_Connect to IP"
			pos			"65 520"
			size		"256 20"
		}

		textentry slot_ip
		{
			string		*cvar:mn_server_ip
			image		"ui/button_green_verysmall"
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"65 540"
			size		"256 34"
		}
		button button_inet_connect_to_ip
		{
			string		"_Connect"
			image		"ui/button_green_small"
			color		"0 0.8 0 1"
			font		"f_small"
			pos			"335 540"
			size		"176 32"
			onClick		{ cmd "server_info *cvar:mn_server_ip;" }
		}
	}

	// ====================
	// init
	// ====================
	func onWindowOpened
	{
		*cvar:cl_serverlist = 0
		cmd "pingservers refresh;"
		call *node:root.set_mn_serverliststr_from_id( *cvar:cl_serverlist )
	}

	func onEvent
	{
		cmd "pingservers refresh;"
	}
}

// ==================
// INTERNET MENU
// ==================

window mp_bookmarks extends abstract_multiplayer_join
{
	panel content {
		{
			pos			"470 120"
			size		"470 580"
		}

		// ====================
		// lan / inet buttons
		// ====================

		string inet_bookmarks
		{
			string		"_Bookmarks"
			font		"f_menubig"
			color		"0 0.5 0 1"
			pos			"15 30"
			size		"440 35"
			textalign	ALIGN_UC
		}

		rows lines {
			pos			"90 95"
			size		"310 480"
			color1		"0 0.08 0 1"
			color2		"0 0.16 0 1"
			lineheight	30
		}
		cmpAdr adr0  { pos "95 100" num 0 }
		cmpAdr adr1  { pos "95 130" num 1 }
		cmpAdr adr2  { pos "95 160" num 2 }
		cmpAdr adr3  { pos "95 190" num 3 }
		cmpAdr adr4  { pos "95 220" num 4 }
		cmpAdr adr5  { pos "95 250" num 5 }
		cmpAdr adr6  { pos "95 280" num 6 }
		cmpAdr adr7  { pos "95 310" num 7 }
		cmpAdr adr8  { pos "95 340" num 8 }
		cmpAdr adr9  { pos "95 370" num 9 }
		cmpAdr adr10 { pos "95 400" num 10 }
		cmpAdr adr11 { pos "95 430" num 11 }
		cmpAdr adr12 { pos "95 460" num 12 }
		cmpAdr adr13 { pos "95 490" num 13 }
		cmpAdr adr14 { pos "95 520" num 14 }
		cmpAdr adr15 { pos "95 550" num 15 }
	}

	confunc check_bookmarks {
		*cvar:i = 0
		while ( *cvar:i <= 15 ) {
			if ( *cvar:adr<cvar:i> ne "" ) {
				*node:root.content.adr<cvar:i>.resetadr@invis = false
				*node:root.content.adr<cvar:i>.adr@invis = false
				*node:root.content.adr<cvar:i>.adr@string = *cvar:adr<cvar:i>
			} else {
				*node:root.content.adr<cvar:i>.resetadr@invis = true
				*node:root.content.adr<cvar:i>.adr@invis = true
			}
			*cvar:i = ( *cvar:i + 1 )
		}
		delete *cvar:i
	}

	func onWindowOpened {
		cmd "check_bookmarks;"
	}
}

// ==================
// SERVER MENU
// ==================

window mp_create_server extends abstract_multiplayer
{

	tab create_natigation {
		{
			pos	"462 200"
			size	"500 40"
			cvar	*cvar:mn_server_config_tab
			onChange {
				call *node:root.mp_update_config_navigation
			}
		}
		option map {
			label	"_Map"
			value	"1"
		}
		option option {
			label	"_Options"
			value	"2"
		}
		option security {
			label	"_Security/registration"
			value	"3"
		}
	}

	panel panel_main {
		{
			pos			"415 80"
			size		"608 100"
		}

		// ===================
		// HOSTNAME
		// ===================
		string current_hostname
		{
			string		"_Hostname:"
			pos			"85 5"
			size		"256 20"
		}
		textentry slot_hostname
		{
			string		*cvar:sv_hostname
			image		"ui/button_green_verysmall"
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"245 0"
			size		"256 34"
		}

		// ===================
		// GAME TYPE
		// ===================
		todo todo1 {
			string	"use a selectbox for the game type"
			pos		"500 40"
		}

		string gametype_str
		{
			string		"_Gametype:"
			pos			"85 40"
			size		"280 20"
		}
		string gametype_value
		{
			string		*cvar:sv_gametype
			pos			"380 40"
			size		"80 20"
			textalign	ALIGN_UC
			onWheelUp	{ cmd "mp_prevgametype;" }
			onWheelDown	{ cmd "mp_nextgametype;" }
		}
		custombutton gametype_dec
		{
			pos			"364 40"
			size		"16 16"
			icon		"icons/prev"
			onClick		{ cmd "mp_prevgametype;" }
			onWheelUp	{ cmd "mp_prevgametype;" }
			onWheelDown	{ cmd "mp_nextgametype;" }
		}
		custombutton gametype_inc
		{
			pos			"460 40"
			size		"16 16"
			icon		"icons/next"
			onClick		{ cmd "mp_nextgametype;" }
			onWheelUp	{ cmd "mp_prevgametype;" }
			onWheelDown	{ cmd "mp_nextgametype;" }
		}

		// ===================
		// TEAMPLAY
		// ===================
		string server_teamplay_str
		{
			string		"_Teamplay:"
			pos			"85 70"
			size		"280 20"
		}
		checkbox server_teamplay_value
		{
			current		*cvar:sv_teamplay
			pos			"410 70"
			size		"20 18"
			image		"ui/checkbox_blue"
		}

	}

	cmpMapPanel panel_map {
		{
			pos		"495 270"
			size	"475 370"
		}

		// ===================
		// GAMETYPE
		// ===================

		string allowedtype_label
		{
			string		"_Allowed gametype:"
			pos			"10 245"
			size		"256 20"
		}

		string string_gametypes
		{
			string		*cvar:mn_mapgametypes
			pos			"170 245"
			size		"256 32"
			font		"f_verysmall"
			longlines	LONGLINES_WRAP
		}
	}

	panel panel_option {
		{
			pos			"475 260"
			size		"460 350"
		}

		// ===================
		// MORALE
		// ===================
		string morale_str
		{
			string		"_Enable morale states:"
			pos			"25 0"
			size		"300 20"
		}
		checkbox morale_value
		{
			current		*cvar:sv_enablemorale
			pos			"350 1"
			size		"20 18"
			image		"ui/checkbox_blue"
		}

		// ===================
		// MAXCLIENTS
		// ===================
		string maxclients_str
		{
			string		"_Max. clients:"
			tooltip		"_Sets the max allowed clients to connect"
			pos			"25 40"
			size		"300 20"
		}
		string maxclients_value
		{
			string		*cvar:sv_maxclients
			pos			"335 40"
			size		"50 20"
			textalign	ALIGN_UC
		}
		spinner maxclients_change
		{
			current		*cvar:sv_maxclients
			image		"ui/spinner_blue"
			pos			"390 40"
			min			2
			max			16
			delta		1
		}

		// ===================
		// G_MAXPERPLAYER
		// ===================
		string maxperplayer_str
		{
			string		"_Max. soldiers per team:"
			tooltip		"_Sets the max allowed soldiers per team"
			pos			"25 60"
			size		"300 20"
		}
		string maxperplayer_value
		{
			string		*cvar:sv_maxsoldiersperteam
			pos			"335 60"
			size		"50 20"
			textalign	ALIGN_UC
		}
		spinner maxperplayer_change
		{
			current		*cvar:sv_maxsoldiersperteam
			image		"ui/spinner_blue"
			pos			"390 60"
			min			1
			max			8
			delta		1
		}

		// =====================
		// MAXSOLDIER PER PLAYER
		// =====================
		string maxsoldierperplayer_str
		{
			string		"_Max. soldiers per player:"
			tooltip		"_Sets the max allowed soldiers per player when in teamplay"
			pos			"25 80"
			size		"300 20"
		}
		string maxsoldierperplayer_value
		{
			string		*cvar:sv_maxsoldiersperplayer
			pos			"335 80"
			size		"50 20"
			textalign	ALIGN_UC
		}
		spinner maxsoldierperplayer_change
		{
			current		*cvar:sv_maxsoldiersperplayer
			image		"ui/spinner_blue"
			pos			"390 80"
			min			1
			max			8
			delta		1
		}

		// ===================
		// MAX PLAYER PER TEAM
		// max is half of sv_maxclients
		// ===================
		string server_maxplayerperteam_str
		{
			string		"_Max. human players per team:"
			pos			"25 100"
			size		"300 20"
		}
		string server_maxplayerperteam_value
		{
			string		*cvar:sv_maxplayersperteam
			pos			"335 100"
			size		"50 20"
			textalign	ALIGN_UC
		}
		spinner maxplayersperteam_change
		{
			current		*cvar:sv_maxplayersperteam
			image		"ui/spinner_blue"
			pos			"390 100"
			min			1
			max			8
			delta		1
		}

		// ===================
		// AI_NUMACTORS
		// ===================
		string ai_numactors_str
		{
			string		"_Max. AI actors:"
			tooltip		"_Max AI actors for teamplay mode"
			pos			"25 120"
			size		"300 20"
		}
		string ai_numactors_value
		{
			string		*cvar:ai_numactors
			pos			"335 120"
			size		"50 20"
			textalign	ALIGN_UC
		}
		spinner ai_numactors_change
		{
			current		*cvar:ai_numactors
			image		"ui/spinner_blue"
			pos			"390 120"
			min			0
			max			8
			delta		1
		}

		// TODO: Allow to set this to 0 to deactivate the round time limit
		// ===================
		// ROUND TIMELIMIT
		// ===================
		string timelimit_str
		{
			string		"_Round time limit in seconds:"
			pos			"25 140"
			size		"250 20"
		}
		string timelimit_value
		{
			string		*cvar:sv_roundtimelimit
			pos			"335 140"
			size		"50 20"
			textalign	ALIGN_UC
		}
		spinner timelimit_change
		{
			current		*cvar:sv_roundtimelimit
			image		"ui/spinner_blue"
			pos			"390 140"
			min			30
			max			500
			delta		5
		}
	}

	panel panel_security {
		{
			pos			"415 250"
			size		"608 768"
		}

		// =====================
		// PUBLIC - MASTERSERVER
		// =====================
		string public_str
		{
			string		"_Make this server public:"
			pos			"85 20"
			size		"280 20"
		}
		checkbox public_value
		{
			current		*cvar:sv_public
			pos			"410 20"
			size		"20 18"
			image		"ui/checkbox_blue"
		}

		// ===================
		// SERVER_PASSWORD
		// ===================
		string current_server_pwd
		{
			string		"_Server-Password:"
			pos			"85 80"
			size		"256 20"
		}
		textentry slot_server_pwd
		{
			string		*cvar:password
			image		"ui/button_green_verysmall"
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"245 75"
			size		"256 34"
			ispassword	true
		}

		// ===================
		// RCON_PASSWORD
		// ===================
		string current_rcon_pwd
		{
			string		"_RCON-Password:"
			pos			"85 140"
			size		"256 20"
		}
		textentry slot_rcon_pwd
		{
			string		*cvar:rcon_password
			image		"ui/button_green_verysmall"
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"245 135"
			size		"256 34"
			ispassword	true
		}

	}

	// ===================
	// FIREWALL TEST
	// ===================

	/** @todo fix it, it can't support string property */
	text firewall_str
	{
		string		"_Make sure that you've opened the TCP port 27910 in your firewall settings"
		pos			"500 530"
		size		"400 100"
		longlines	LONGLINES_WRAP
		textalign	ALIGN_UC
		font		"f_small"
		dataid		TEXT_STANDARD
	}

	// ====================
	// create_server button
	// ====================

	cmpMenuButton button_server
	{
		string		"_Start server"
		pos			"570 630"
		onClick		{ cmd "mp_startserver;" }
	}

	// ===================
	// Interaction
	// ===================

	func mp_update_config_navigation {
		*node:root.panel_map@invis = true
		*node:root.panel_option@invis = true
		*node:root.panel_security@invis = true
		if ( *cvar:mn_server_config_tab == 1 ) {
			*node:root.panel_map@invis = false
		}
		elif ( *cvar:mn_server_config_tab == 2 ) {
			*node:root.panel_option@invis = false
		}
		elif ( *cvar:mn_server_config_tab == 3 ) {
			*node:root.panel_security@invis = false
		}
	}

	func onWindowOpened
	{
		cmd "killserver;set sv_maxplayersperteam 8;set sv_roundtimelimit 0;set mn_serverday 1;"
		cmd "mp_updategametype;mn_getmaps;"
		*cvar:mn_server_config_tab = 1
		call *node:root.mp_update_config_navigation
		cmd "ui_resetdata TEXT_STANDARD;"
	}
}

// ===========================================
// MULTIPLAYER TEAM SELECTION AND WAIT DIALOG
// ===========================================
window multiplayer_wait extends levelbuttons
{
	{
		timeout 	2000
		fill		true
		starlayout	true
	}

	battlescape render {
		pos			"0 0"
		size		"1024 768"
		align		LAYOUTALIGN_FILL
	}

	todo widescreen { pos "80 370" string "Center panels"}

	panel floor {
		{
			pos		"0 128"
			size	"40 346"
			align	LAYOUTALIGN_LEFT
			excluderect	{ pos "0 0" size "40 4" }
			excluderect	{ pos "6 341" size "35 6" }
			excluderect	{ pos "32 333" size "8 8" }
		}

		image bar_floor
		{
			src	"hud/bar_floor"
			pos		"0 0"
			size	"40 346"
			texh	"40 346"
			ghost	true
		}

		radiobutton bt0
		{
			tooltip	"_Level 1"
			pos		"6 293"
			size	"27 35"
			background	"buttons/blue2-1"
			value	0
			cvar	*cvar:cl_worldlevel
			icon	"icons/number_1"
		}

		radiobutton bt1
		{
			tooltip	"_Level 2"
			pos		"6 253"
			size	"27 35"
			background	"buttons/blue2-1"
			value	1
			cvar	*cvar:cl_worldlevel
			icon	"icons/number_2"
		}

		radiobutton bt2
		{
			tooltip	"_Level 3"
			pos		"6 213"
			size	"27 35"
			background	"buttons/blue2-1"
			value	2
			cvar	*cvar:cl_worldlevel
			icon	"icons/number_3"
		}

		radiobutton bt3
		{
			tooltip	"_Level 4"
			pos		"6 173"
			size	"27 35"
			background	"buttons/blue2-1"
			value	3
			cvar	*cvar:cl_worldlevel
			icon	"icons/number_4"
		}

		radiobutton bt4
		{
			tooltip	"_Level 5"
			pos		"6 133"
			size	"27 35"
			background	"buttons/blue2-1"
			value	4
			cvar	*cvar:cl_worldlevel
			icon	"icons/number_5"
		}

		radiobutton bt5
		{
			tooltip	"_Level 6"
			pos		"6 93"
			size	"27 35"
			background	"buttons/blue2-1"
			value	5
			cvar	*cvar:cl_worldlevel
			icon	"icons/number_6"
		}

		radiobutton bt6
		{
			tooltip	"_Level 7"
			pos		"6 53"
			size	"27 35"
			background	"buttons/blue2-1"
			value	6
			cvar	*cvar:cl_worldlevel
			icon	"icons/number_7"
		}

		radiobutton bt7
		{
			tooltip	"_Level 8"
			pos		"6 13"
			size	"27 35"
			background	"buttons/blue2-1"
			value	7
			cvar	*cvar:cl_worldlevel
			icon	"icons/number_8"
		}
	}

	panel players {
		{
			pos		"520 30"
			size	"400 250"
			padding	5
			image	"ui/panel_window"
		}

		string title
		{
			string		"_Players connected"
			font		"f_big"
			color		"1 1 1 1"
			pos			"0 5"
			size		"415 50"
			textalign	ALIGN_CC
		}

		panel ready {
			{
				layout LAYOUT_TOP_DOWN_FLOW
				layoutMargin 2
				padding		0
				pos			"50 50"
				size		"16 300"
			}

			image p0 { size "16 16" }
			image p1 { size "16 16" }
			image p2 { size "16 16" }
			image p3 { size "16 16" }
			image p4 { size "16 16" }
			image p5 { size "16 16" }
			image p6 { size "16 16" }
			image p7 { size "16 16" }
			image p8 { size "16 16" }
			image p9 { size "16 16" }
		}

		text userlist
		{
			dataid		TEXT_MULTIPLAYER_USERLIST
			pos			"80 50"
			size		"150 300"
		}

		text teamlist
		{
			dataid		TEXT_MULTIPLAYER_USERTEAM
			pos			"200 50"
			size		"150 300"
		}
	}

	panel teamselection {
		{
			pos		"520 290"
			size	"400 200"
			padding	10
			image	"ui/panel_window"
		}

		string title
		{
			string		"_Team selection"
			font		"f_big"
			color		"1 1 1 1"
			pos			"0 5"
			size		"415 50"
			textalign	ALIGN_CC
		}

		text teamnum_status
		{
			dataid		TEXT_STANDARD
			pos			"10 50"
			size		"380 32"
			lineheight	16
			tabwidth	60
			rows		2
			textalign	ALIGN_CC
		}

		string teamnum_str
		{
			string		"_Team preference:"
			pos			"25 80"
			size		"205 20"
		}
		string teamnum_value
		{
			string		*cvar:cl_teamnum
			pos			"240 80"
			size		"40 20"
			textalign	ALIGN_UR
		}
		button teamnum_inc
		{
			visiblewhen	"*cvar:cl_ready != 1"
			tooltip		"_Next free team"
			icon		"icons/next"
			pos			"298 82"
			size		"16 16"
			padding		0
			onClick		{ cmd "teamnum_inc;" }
		}
		button teamnum_dec
		{
			visiblewhen	"*cvar:cl_ready != 1"
			tooltip		"_Previous free team"
			icon		"icons/prev"
			pos			"314 82"
			size		"16 16"
			padding		0
			onClick		{ cmd "teamnum_dec;" }
		}

		/** @todo only for admin, and command it self should check the user */
		button button_force
		{
			visiblewhen "*cvar:cl_admin == 1"
			string		"_Force start"
			tooltip		"_Start the game even if not all teams is ready or have joined"
			image		"ui/button_blue"
			pos			"35 130"
			size		"150 30"
			color		"0.8 0.8 0.8 1"
			selectcolor "1 1 1 1"
			onClick		{ cmd "startgame;" }
		}

		checkbox cb_ready
		{
			tooltip		"_Finish the teamselection and start the game"
			image		"ui/checkbox_green"
			pos			"200 130"
			size		"20 20"
			current		*cvar:cl_ready
		}
		string cb_ready_label
		{
			string		"_Are you ready?"
			tooltip		"_Finish the teamselection and start the game"
			pos			"230 130"
			size		"180 20"
			onClick		{ call *node:parent.cb_ready@toggle }
		}
	}

	button swap_irc_connection_status {
		string		"_Activate IRC"
		image		"ui/button_blue"
		pos			"30 550"
		size		"150 40"
		color		"0.8 0.8 0.8 1"
		selectcolor "1 1 1 1"
		onClick {
			if ( *cvar:mn_ircconnected == 1 ) {
				cmd "irc_disconnect;"
				*cvar:mn_ircconnected = 0
				*node:this@string = "_Activate IRC"
				*node:parent.swap_irc_users_visibility@disabled = true
				*node:parent.invite_players@disabled = true
			} else {
				cmd "irc_connect;"
				*cvar:mn_ircconnected = 1
				*node:this@string = "_Deactivate IRC"
				*node:parent.swap_irc_users_visibility@disabled = false
				*node:parent.invite_players@disabled = false
			}
		}
	}

	button swap_irc_users_visibility {
		string		"_Display IRC users"
		image		"ui/button_blue"
		pos			"30 600"
		size		"150 40"
		disabled	true
		color		"0.8 0.8 0.8 1"
		selectcolor "1 1 1 1"
		onClick {
			if ( *node:parent.allchats.ircscreen.users@invis == true ) {
				*node:parent.allchats.ircscreen.users@invis = false
				*node:this@string = "_Display IRC users"
			} else {
				*node:parent.allchats.ircscreen.users@invis = true
				*node:this@string = "_Hide IRC users"
			}
		}
	}

	button invite_players {
		string		"_Invite others"
		image		"ui/button_blue"
		tooltip		"_Invite players that are online in the lobby"
		pos			"30 650"
		size		"150 40"
		disabled	true
		color		"0.8 0.8 0.8 1"
		selectcolor "1 1 1 1"
		onClick {
			cmd "irc_invite;"
		}
	}

	panel allchats {
		{
			pos				"200 550"
			size			"800 200"
			layout			LAYOUT_BORDER
			padding	18
			image	"ui/panel_window"
		}

		tab selectchat {
			{
				align	LAYOUTALIGN_TOP
				height	40
				cvar	*cvar:mn_chatselect
				onChange {
					if ( *cvar:mn_chatselect eq "irc" ) {
						*node:parent.ircscreen@invis = false
						*node:parent.chatscreen@invis = true
					}
					if ( *cvar:mn_chatselect eq "chat" ) {
						*node:parent.ircscreen@invis = true
						*node:parent.chatscreen@invis = false
					}
				}
			}

			option chat {
				label	"_Party chat"
				value	"chat"
			}
			option irc {
				label	"_Global lobby (IRC)"
				value	"irc"
			}
		}

		panel ircscreen {
			{
				layout			LAYOUT_BORDER
				align			LAYOUTALIGN_MIDDLE
				layoutMargin	5
				padding			5
				bgcolor			"0 0.1 0 0.7"
				invis			true
			}

			panel users {
				{
					width			"200"
					layout			LAYOUT_BORDER
					align			LAYOUTALIGN_RIGHT
					layoutMargin	5
					padding			0
					invis			true
				}

				textlist list
				{
					lineheight	22
					dataid		TEXT_IRCUSERS
					rows		"27"
					onClick		{ cmd "irc_userlist_click <lineselected>;" }
					onRClick	{ cmd "irc_userlist_rclick <lineselected>;" }
					onViewChange {
						*node:parent.scroll@fullsize = <fullsize>
						*node:parent.scroll@current = <viewpos>
						*node:parent.scroll@viewsize = <viewsize>
					}
				}

				vscrollbar scroll {
					image		"ui/scrollbar_v_green"
					align		LAYOUTALIGN_RIGHT
					onChange 	{
						*node:parent.list@viewpos = <current>
					}
				}
			}

			text irc
			{
				dataid		TEXT_IRCCONTENT
				align		LAYOUTALIGN_MIDDLE
				font		"f_verysmall"
				rows		"10"
				lineheight	"16"
				padding		5
				onViewChange {
					*node:parent.irc_scroll@fullsize = <fullsize>
					*node:parent.irc_scroll@viewsize = <viewsize>
					*node:parent.irc_scroll@current = <viewpos>
				}
			}

			vscrollbar irc_scroll {
				image		"ui/scrollbar_v_green"
				align		LAYOUTALIGN_RIGHT
				onChange 	{
					*node:parent.irc@viewpos = <current>
				}
			}

			panel input {
				{
					layout			LAYOUT_PACK
					layoutMargin	5
					align			LAYOUTALIGN_BOTTOM
					height			25
				}

				string say_label {
					align		LAYOUTALIGN_LEFT
					width		"50"
					textalign	ALIGN_CL
					string		"_Say:"
				}

				textentry say {
					align		LAYOUTALIGN_FILL
					image		"ui/button_green_verysmall"
					string		*cvar:irc_send_buffer
					color 		"0 .78 0 1"
					selectcolor	"1 1 1 1"
					textalign	ALIGN_CL
					padding		8
					font		"f_verysmall"
					clickoutabort	true
					onChange	{
						cmd "irc_chanmsg;"
						cmd "multiplayer_ircsay_edit;"
					}
				}

				/** @todo move it into say@onChange
				 * But at the moment this call into that callback dont work
				 * without an intermediat command
				 */
				confunc multiplayer_ircsay_edit {
					call *node:parent.say@edit
				}
			}
		}

		panel chatscreen {
			{
				layout			LAYOUT_BORDER
				align			LAYOUTALIGN_MIDDLE
				layoutMargin	5
				padding			5
				bgcolor			"0 0.1 0 0.7"
			}

			text chat
			{
				dataid		TEXT_CHAT_WINDOW
				align		LAYOUTALIGN_MIDDLE
				font		"f_verysmall"
				rows		"10"
				lineheight	"16"
				padding		5
				onViewChange {
					*node:parent.chat_scroll@fullsize = <fullsize>
					*node:parent.chat_scroll@viewsize = <viewsize>
					*node:parent.chat_scroll@current = <viewpos>
				}
			}

			vscrollbar chat_scroll {
				image		"ui/scrollbar_v_green"
				align		LAYOUTALIGN_RIGHT
				onChange 	{
					*node:parent.chat@viewpos = <current>
				}
			}

			panel input {
				{
					layout			LAYOUT_PACK
					layoutMargin	5
					align			LAYOUTALIGN_BOTTOM
					height			25
				}

				string say_label {
					align		LAYOUTALIGN_LEFT
					width		"50"
					textalign	ALIGN_CL
					string		"_Say:"
				}

				textentry say {
					align		LAYOUTALIGN_FILL
					image		"ui/button_green_verysmall"
					string		*cvar:mn_entry
					color 		"0 .78 0 1"
					selectcolor	"1 1 1 1"
					textalign	ALIGN_CL
					padding		8
					font		"f_verysmall"
					clickoutabort	true
					onChange	{
						cmd "say <cvar:mn_entry>"
						*cvar:mn_entry = ""
						cmd "multiplayer_chatsay_edit;"
					}
				}

				/** @todo move it into say@onChange
				 * But at the moment this call into that callback dont work
				 * without an intermediat command
				 */
				confunc multiplayer_chatsay_edit {
					call *node:parent.say@edit
				}
			}
		}
	}

	/**
	 * @param <1> player ID/line
	 * @param <2> 0: not ready, 1: ready
	 * @todo use an image instead of a color
	 */
	confunc multiplayer_playerIsReady {
		if ( ( <1> < 10 ) and ( <1> >= 0 ) ) {
			if ( <2> == 0 ) {
				*node:root.players.ready.p<1>@src = "icons/off"
				*node:root.players.ready.p<1>@invis = false
			} else {
				*node:root.players.ready.p<1>@src = "icons/on"
				*node:root.players.ready.p<1>@invis = false
			}
		}
	}

	/**
	 * @param <1> number of players
	 */
	confunc multiplayer_playerNumber {
		*cvar:tmp = "<1>"
		while ( *cvar:tmp < 10 ) {
			*node:root.players.ready.p<1>@invis = true
			*cvar:tmp = ( *cvar:tmp + 1 )
		}
		delete *cvar:tmp
	}

	/**
	 * @param <1> boolean whether this is a teamplay match
	 */
	confunc multiplayer_setTeamplay {
		// @todo - deactivate team change if the given boolean is false	(0)
	}

	/**
	 * @inherited from levelbuttons window
	 * @param <1> level ID (0..7)
	 * @param <2> status: 0 not available, 1 available, 2 selected
	 */
	confunc updateLevelStatus {
		if ( <2> == 0 ) { *node:root.floor.bt<1>@disabled = true }
		elif ( <2> >= 1 ) { *node:root.floor.bt<1>@disabled = false }
	}

	// ===================
	// EVENTS
	// ===================

	// update every 2 sec
	func onEvent
	{
		cmd "mp_selectteam_init;"
	}

	func onWindowOpened
	{
		cmd "mp_selectteam_init;"
		*cvar:mn_displaypartyinfo = 1
		*cvar:mn_entry = ""
		*cvar:mn_chatselect = "chat"
		*cvar:cl_ready = 0
	}

	func onWindowClosed
	{
		delete *cvar:mn_entry
		delete *cvar:mn_chatselect
		delete *cvar:mn_displaypartyinfo
	}
}

// ==========================
// SERVER PASSWORD
// ==========================
window serverpassword
{
	{
		string		"_Enter server password"
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		closebutton	true
		image		"ui/popup_alpha_tile"
	}

	// ===================
	// PASSWORD
	// ===================
	string current_password
	{
		string		"_Password:"
		pos			"66 58"
		size		"256 20"
	}
	textentry slot_password
	{
		image		"ui/button_green_verysmall"
		color		"0 .78 0 1"
		selectcolor	"1 1 1 1"
		textalign	ALIGN_CL
		padding		8
		font		"f_verysmall"
		pos			"66 78"
		size		"256 34"
		string		*cvar:password
		ispassword	true
	}

	button set_password
	{
		string		"_OK"
		tooltip		"_Connect"
		image		"ui/button_green_small"
		pos			"260 170"
		width		"200"
		font		"f_menu"
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "mn_server_need_password 0;ui_pop;connect;" }
	}
}

// ==========================
// CHAT POPUP (IRC)
// ==========================
window chat_popup
{
	{
		string		"_Personal message"
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		closebutton	true
		image		"ui/popup_alpha_tile"
	}

	text chatscreen
	{
		dataid			TEXT_CHAT_WINDOW
		pos			"26 58"
		size		"444 100"
		lineheight	20
		rows		5
		tabwidth	150
		font		"f_verysmall"
	}

	button enter_lobby
	{
		string		"_Lobby"
		tooltip		"_Go into the lobby"
		image		"ui/button_green_small"
		pos			"260 170"
		width		"200"
		font		"f_menu"
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "ui_pop;ui_push irc;" }
	}
}

// ==========================
// GAME INVITATION
// ==========================
window multiplayer_invite
{
	{
		string		"_Game invitation"
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		closebutton	true
		image		"ui/popup_alpha_tile"
	}

	/* store popup data */
	string ip { invis true }
	string port { invis true }

	confunc	multiplayer_invite_server_info {
		*node:root.ip@string = <1>
		*node:root.port@string = <2>
	}

	button server_info
	{
		string		"_Server info"
		tooltip		"_Show server information"
		image		"ui/button_green_small"
		pos			"30 170"
		width		"200"
		font		"f_menu"
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{
			cmd "ui_pop;"
			cmd "server_info <node:root.ip@string> <node:root.port@string>;"
		}
	}

	button enter_lobby
	{
		string		"_Lobby"
		tooltip		"_Go into the lobby"
		image		"ui/button_green_small"
		pos			"260 170"
		width		"200"
		font		"f_menu"
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "ui_pop;ui_push irc;" }
	}

	string invite_msg
	{
		string		"_You received a invitation to take part of a multiplayer game. Hit the connect button to connect to the server"
		pos			"30 50"
		longlines	LONGLINES_WRAP
		size		"440 60"
	}
}
