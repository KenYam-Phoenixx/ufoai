// ==================
// TEAM MENU
// ==================

component cmpDefNamedTbar cmpNamedTbar {
	{
		size	"450 20"
	}
	pic			bdr { 
		pos "165 2"
	}
	tbar		bar {
		pos "167 2"
	}
	string	val {
		pos "265 0"
		size "150 20"
	}
}

component panel cmpNamedBar {
	{
		size	"450 20"
	}
	pic			bdr { 
		pos "165 2"
		image ui/bar_border 
	}
	bar		currbar {
		pos "167 7"
		size "255 6"
		color		".5 .9 .5 1"
		max 100
	}
	bar		initbar {
		pos "167 7"
		size "255 6"
		color		"0 .8 0 1"
		max 100
	}
	string	lbl {
		pos "0 0"
		size "180 20"
		string ""
	}
	string	val {
		pos "265 0"
		size "150 20"
		textalign ur
	}
}


component cmpActorSelect cmpSoldierSelect {
	{
		num "0"
	}

	button bt_equip
	{
		pos			"0 10"
		size		"16 16"
		padding		0
		invis		true
		onWheelUp 	{ cmd "mn_active_vscrollbar <path:root>.scroll_employees 0;" } // 0: click on top
		onWheelDown { cmd "mn_active_vscrollbar <path:root>.scroll_employees 4;" } // 4: click on bottom
	}
	checkbox check_actor
	{
		tooltip	"_Add/remove this soldier"
		onChange { cmd "team_hire <node:parent@num> <cvar:mn_relative_id>;" }
	}

	string txt_actor
	{
		string	*cvar:mn_ename<node:parent@num>
		onClick { cmd "team_select <node:parent@num> <cvar:mn_relative_id>;" }
	}

}


/**
 * @todo rename the menu according to the file
 */
window team extends iaircraft
{
	// ==================
	// background
	// ==================

	panel actors_background {
		pos		"18 443"
		size	"329 321"
		image	"ui/panel_grey_green_large"
	}

	panel tab_background {
		pos		"24 386"
		size	"986 56"
		image	"ui/panel_tab"
	}
	// ==================
	// aircraft
	// ==================
	model aircraft
	{
		model		*cvar:mn_aircraft_model
		pos			"478 107"
		size		"529 260"
		origin		"-45 25 0"
		angles		"160 20 -120"
		autoscale	true
	}

	string team_members
	{
		string	"_Team Members:"
		pos		"480 107"
		size	"200 20"
	}

	string team_hired
	{
		string		*cvar:mn_hired
		pos			"619 107"
		size		"62 20"
		textalign	ur
	}

	func listen_close_equipment
	{
		cmd "team_select <cvar:cl_selected2>"
		cmd "del cl_selected2"
		cmd "mn_removelistener equipment@onClose <path:this>"
	}

	button bt_equipsoldiers
	{
		string "_Equip soldiers"
		pos		"483 129"
		size	"198 43"
		image	ui/button_blue
		onClick	{
			*cvar:cl_selected2 = <cvar:cl_selected>
			cmd "mn_addlistener equipment@onClose <path:root>.listen_close_equipment;"
			cmd "mn_push equipment;"
		}
		color	"1 1 1 0.8"
		selectcolor "1 1 1 1"
	}
	todo equip_soldiers_subtab {
		pos "463 135"
		string "It would be nice to create a mockup and prototype to equip soldiers with another aircraft tab - should be a very nice short cut"
	}

	// ==================
	// names list
	// ==================

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_soldier_usedelsewhere
	{
		*actor_select<1>.txt_actor@color = ".50 .50 .50 1"
		*(checkbox)actor_select<1>.check_actor@current = 0
		*(checkbox)actor_select<1>.check_actor@tooltip = "_Soldier is assigned to another craft"
		*actor_select<1>.check_actor@disabled = true
		*actor_select<1>.check_actor@invis = false
		*actor_select<1>.txt_actor@invis = false
		*(pic)actor_select<1>.bt_equip@invis = false
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_soldier_unassigned {
		*actor_select<1>.txt_actor@color = "0 .78 0 1"
		*actor_select<1>.check_actor@disabled = false
		*(checkbox)actor_select<1>.check_actor@current = 0
		*actor_select<1>.check_actor@invis = false
		*actor_select<1>.txt_actor@invis = false
		*(pic)actor_select<1>.bt_equip@invis = false
		if ( *cvar:mn_hirable_count == 0 ) {
			*(checkbox)actor_select<1>.check_actor@disabled = true
			*(checkbox)actor_select<1>.check_actor@tooltip = "_No more soldiers can be assigned to this craft"
		} else {
			*(checkbox)actor_select<1>.check_actor@tooltip = "_Assign soldier to this craft"
		}
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_soldier_assigned {
		*actor_select<1>.txt_actor@color = "0 .78 0 1"
		*actor_select<1>.check_actor@disabled = false
		*(checkbox)actor_select<1>.check_actor@current = 1
		*(checkbox)actor_select<1>.check_actor@tooltip = "_Remove soldier from this craft"
		*actor_select<1>.check_actor@invis = false
		*actor_select<1>.txt_actor@invis = false
		*(pic)actor_select<1>.bt_equip@invis = false
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_soldier_holdsequip {
		*(pic)actor_select<1>.bt_equip@tooltip = "_Soldier carries equipment"
		*(pic)actor_select<1>.bt_equip@icon = "soldier_holdequipment"
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_soldier_holdsnoequip {
		*(pic)actor_select<1>.bt_equip@tooltip = "_Soldier carries no equipment"
		*(pic)actor_select<1>.bt_equip@icon = "soldier_noequipment"
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_soldier_unusedslot {
		*(pic)actor_select<1>.bt_equip@invis = true
		*actor_select<1>.check_actor@invis = true
		*actor_select<1>.txt_actor@invis = true
	}

	/**
	 * @param[in] <1> number of employee in the list
	 */
	confunc aircraft_soldier_list_size {
		*(abstractscrollbar)scroll_employees@fullsize = <1>
	}

	/**
	 * @param[in] <1> number of employee in the list
	 */
	confunc aircraft_soldier_selected {
		*actor_select<1>.txt_actor@color = "1 1 1 1"
	}

	cmpSoldierSelect actor_select0 {
		pos "38 470"
	}

	cmpSoldierSelect actor_select1 {
		pos "38 505"
	}

	cmpSoldierSelect actor_select2
	{
		pos			"38 540"
	}

	cmpSoldierSelect actor_select3
	{
		pos			"38 575"
	}

	cmpSoldierSelect actor_select4
	{
		pos			"38 610"
	}

	cmpSoldierSelect actor_select5
	{
		pos			"38 645"
	}

	cmpSoldierSelect actor_select6
	{
		pos			"38 680"
	}

	cmpSoldierSelect actor_select7
	{
		pos			"38 715"
	}
	vscrollbar scroll_employees
	{
		image	ui/scrollbar_v_green
		pos "310 463"	height	"282"
		current 0
		viewsize 8
		fullsize 50
		onChange {
			cmd "update_soldier_list 8 <current>"
			*cvar:mn_relative_id = <current>
		}
	}

	// ==================
	// actor info
	// ==================

	panel actor_background {
	{
		pos		"358 443"
		size	"663 321"
		image	"ui/panel_grey_black"
	}

	panel actormodel_background {
	{
		pos		"12 12"
		size	"179 296"
		image	"ui/panel_green"
	}

	pic	rank_img {
		pos "8 10"
		size "50 50"
		image *cvar:mn_chrrank_img
	}

	model body
	{
		{
			model	*cvar:mn_body
			skin	*cvar:mn_skin
			anim	"stand2"
			angles	"0 70 90"
			scale	"5.5 5.5 5.5"
			pos		"8 10"
			size	"167 278"
			origin	"0 -35 0"
		}

		model head
		{
			model	*cvar:mn_head
			skin	*cvar:mn_skin
			tag		"tag_head"
			scale	"1.0 1.0 1.0"
		}
	
		model right_rweapon
		{
			model	*cvar:mn_rweapon
			tag		"tag_rweapon"
		}
	
		model right_lweapon
		{
			model	*cvar:mn_lweapon
			tag		"tag_lweapon"
		}
	}

	string	name { pos "10 245" size "165 20" string *cvar:mn_name }
	string	rank_lbl { pos "10 265" size "165 20" string *cvar:mn_chrrank }
	}

	tab actor_stat {
		pos		"212 12"
		size	"432 39"
		cvar	*cvar:mn_actor_stat
		onChange {
			cmd "select_actor_stat <cvar:mn_actor_stat>;"
		}
		option phys {
			label	"_Physical stats"
			value	"stats_phys"
		}
		option missions {
			label	"_Missions info"
			value	"stats_missions"
		}
	}


	panel stats_phys {
	{
		pos		"208 44"
		size	"440 264"
		image	"ui/panel_green_tab"
	}
	cmpNamedBar pwr {
		{
			pos "9 27"
		}
		bar currbar { current *cvar:mn_vpwr }
		bar initbar { current *cvar:mn_vpwri }
		string lbl  { string "_Strength:" }
		string val  { string *cvar:mn_tpwr }
	}
	cmpNamedBar spd {
	{
		pos "9 50"
	}
		bar currbar { current *cvar:mn_vspd }
		bar initbar { current *cvar:mn_vspdi }
		string lbl  { string "_Speed:" }
		string val  { string *cvar:mn_tspd }
	}
	cmpNamedBar acc {
	{
		pos "9 73"
	}
		bar currbar { current *cvar:mn_vacc }
		bar initbar { current *cvar:mn_vacci }
		string lbl  { string "_Accuracy:" }
		string val  { string *cvar:mn_tacc }
	}
	cmpNamedBar mnd {
	{
		pos "9 96"
	}
		bar currbar { current *cvar:mn_vmnd }
		bar initbar { current *cvar:mn_vmndi }
		string lbl  { string "_Mind:" }
		string val  { string *cvar:mn_tmnd }
	}
	cmpNamedBar cls {
	{
		pos "9 119"
	}
		bar currbar { current *cvar:mn_vcls }
		bar initbar { current *cvar:mn_vclsi }
		string lbl  { string "_Close Combat:" }
		string val  { string *cvar:mn_tcls }
	}

	cmpNamedBar hvy {
	{
		pos "9 142"
	}
		bar currbar { current *cvar:mn_vhvy }
		bar initbar { current *cvar:mn_vhvyi }
		string lbl  { string "_Heavy Weapons:" }
		string val  { string *cvar:mn_thvy }
	}

	cmpNamedBar ass {
	{
		pos "9 165"
	}
		bar currbar { current *cvar:mn_vass }
		bar initbar { current *cvar:mn_vassi }
		string lbl  { string "_Assault Weapons:" }
		string val  { string *cvar:mn_tass }
	}

	cmpNamedBar snp {
	{
		pos "9 188"
	}
		bar currbar { current *cvar:mn_vsnp }
		bar initbar { current *cvar:mn_vsnpi }
		string lbl  { string "_Sniper Rifles:" }
		string val  { string *cvar:mn_tsnp }
	}

	cmpNamedBar exp {
	{
		pos "9 211"
	}
		bar currbar { current *cvar:mn_vexp }
		bar initbar { current *cvar:mn_vexpi }
		string lbl  { string "_High-Explosives:" }
		string val  { string *cvar:mn_texp }
	}

	cmpNamedTbar hp {
	{
		pos "9 234"
	}
		tbar bar   {
			current *cvar:mn_vhp
			max *cvar:mn_vhpmax
			image ui/bar_health
			}
		string lbl { string "_Health:" }
		string val { string *cvar:mn_thp }
	}

	}

	panel stats_missions {
	{
		pos		"208 44"
		size	"440 264"
		image	"ui/panel_green_tab"
		invis true
	}

	pic	rank_img {
		pos "195 30"
		size "50 50"
		image *cvar:mn_chrrank_img
	}

	string	rank_lbl { pos "10 85" size "420 20" string *cvar:mn_chrrank textalign uc font f_normal_bold }

	string	missions_lbl { pos "12 150" size "200 20" string "_Missions performed:" }
	string	missions_val { pos "212 150" size "150 20" string *cvar:mn_chrmis textalign ur }

	string	kills_lbl { pos "12 170" size "200 20" string "_Enemies killed:" }
	string	kills_val { pos "212 170" size "150 20" string *cvar:mn_chrkillalien textalign ur }

	string	civ_lbl { pos "12 190" size "200 20" string "_Civilians killed:" }
	string	civ_val { pos "212 190" size "150 20" string *cvar:mn_chrkillcivilian textalign ur }

	string	team_lbl { pos "12 210" size "200 20" string "_Our team killed:" }
	string	team_val { pos "212 210" size "150 20" string *cvar:mn_chrkillteam textalign ur }

	}  

	}


	/** @todo Activate this again - used for UGVs */
	// ==================
	// additional stuff
	// ==================
	confunc toggle_show_heavybutton {
		*bt_switch_list@tooltip	= "_Assign tanks"
		*bt_switch_list@icon	= uvc
	}
	confunc toggle_show_soldiersbutton {
		*bt_switch_list@tooltip	= "_Assign soldiers"
		*bt_switch_list@icon	= soldier
	}

	/**
	 * @note call when tab actor_stat changed.
	 */
	confunc select_actor_stat {
		if ( <1> eq "stats_phys" ) {
			*actor_background.stats_phys@invis = false
			*actor_background.stats_missions@invis = true
		}
		elif ( <1> eq "stats_missions" ) {
			*actor_background.stats_phys@invis = true
			*actor_background.stats_missions@invis = false
		}

	
	}

	/**
	 * @note call when the aircraft status change
	 */
	confunc aircraft_status_change
	{
		if ( *cvar:mn_hired_count > 0 ) {
			*bt_equipsoldiers@disabled = false
			*bt_equipsoldiers@tooltip = "_Equip soldiers"
		} else {
			*bt_equipsoldiers@disabled = true
			*bt_equipsoldiers@tooltip = "_No soldiers assigned"
		}
	}

	/**
	 * @note iaircraft interface
	 */
	confunc aircraft_change
	{
		cmd "update_soldier_list 8 0;team_select 0;set mn_show_employee 1;"
		*scroll_employees@current = 0
		*aircraft_list@lineselected = <1>
		// absent from base, go back to the main GUI
		if ( *cvar:mn_equipsoldierstate == 1 ) {
			cmd "mn_pop;mn_push aircraft;"
		}
		cmd "aircraft_status_change"
	}

	func onInit
	{
		cmd "mn_select_aircraft <cvar:mn_aircraft_id>"
		*cvar:i = 0
		while ( *cvar:i < 8 ) {
			*actor_select<cvar:i>@num = <cvar:i>
			*actor_select<cvar:i>.txt_actor@string = *cvar:mn_ename<cvar:i>
			*cvar:i = ( *cvar:i + 1 )
		}
		delete *cvar:i

		*scroll_employees@current = 0
		*cvar:mn_relative_id = 0
		cmd "update_soldier_list 8 0;team_select 0;set mn_show_employee 1;"
		cmd "aircraft_status_change"
	}
}
