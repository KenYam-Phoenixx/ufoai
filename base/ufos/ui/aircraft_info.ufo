// ==================
// MAIN AIRCRAFT MENU
// ==================

window aircraft extends iaircraft
{
	// ==================
	// background
	// ==================

	panel info_background {
		pos		"461 444"
		size	"561 320"
		image	"ui/panel_grey_green_large"
	}

	// ==================
	// aircraft
	// ==================
	model aircraft
	{
		src			*cvar:mn_aircraft_model
		pos			"478 107"
		size		"529 260"
		angles		"0 0 90"
		autoscale	true
		rotatewithmouse	true
	}

	string aircraft_name
	{
		string		*cvar:mn_aircraftname
		pos			"505 480"
		size		"317 21"
		textalign	ALIGN_UL
		padding		0
	}

	string aircraft_status_label
	{
		string		"_Status:"
		pos			"505 510"
		size		"210 21"
		textalign	ALIGN_UL
		padding		0
	}

	string aircraft_status
	{
		string		*cvar:mn_aircraftstatus
		pos			"770 510"
		size		"229 20"
		textalign	ALIGN_UL
	}

	custombutton aircraft_return
	{
		pos			"726 510"
		icon		"icons/aircraft_return"
		size		"16 16"
	}

	string aircraft_health_label
	{
		string		"_Health:"
		pos			"505 540"
		size		"210 21"
		textalign	ALIGN_UL
		padding		0
	}

	image aircraft_healthbarborder {
		pos "724 540"
		src "ui/bar_border"
	}
	bar aircraft_healthbar {
		pos			"726 546"
		size		"249 5"
		min			0
		max			196
		readonly	true
	}

	//	icons below the aircraft

	// ==================
	// additional stuff
	// ==================

	string team_members
	{
		string		"_Team Members:"
		pos			"505 570"
		size		"210 20"
		textalign	ALIGN_CL
		padding		0
	}

	string team_hired
	{
		string		*cvar:mn_hired
		pos			"722 570"
		size		"110 20"
		textalign	ALIGN_CL
		padding		0
	}

	text aircraftinfo
	{
		pos			"505 600"
		size		"495 140"
		lineheight	20
		tabwidth	220
		dataid		TEXT_AIRCRAFT_INFO
		rows		7
	}

	confunc aircraft_checkbuttons
	{
		if ( *cvar:mn_aircraftinbase > 0 ) {
			*node:root.aircraft_navigation@disabled = false

			*node:root.aircraft_return@tooltip = "_Start Aircraft"
			*node:root.aircraft_return@onClick = { cmd "aircraft_start;" }
			*node:root.aircraft_return@texl = "0 32"
		} else {
			*node:root.aircraft_navigation@disabled = true

			*node:root.aircraft_return@tooltip = "_Call back"
			*node:root.aircraft_return@onClick = { cmd "aircraft_return;" }
			*node:root.aircraft_return@texl = "0 0"
		}

		// absent from base
		if ( *cvar:mn_equipsoldierstate == 1 ) {
			*node:root.aircraft_navigation@disabled = true
		}
		// no soldiers
		elif ( *cvar:mn_equipsoldierstate == 2 ) {
			*node:root.aircraft_navigation@disabled = false
		}
		// normal
		elif ( *cvar:mn_equipsoldierstate == 3 ) {
			*node:root.aircraft_navigation@disabled = false
		}
	}

	func aircraft_healtbarchange {
		*node:root.aircraft_healthbar@tooltip = "<cvar:mn_aircraft_health>%"
		*node:root.aircraft_healthbar@current = ( ( <cvar:mn_aircraft_health> / 100 ) * <node:root.aircraft_healthbar@max> )
		if ( *cvar:mn_aircraft_health <= 33 ) {
			*node:root.aircraft_healthbar@color = "1 .0 .0 1"
		} elif ( *cvar:mn_aircraft_health <= 75 ) {
			*node:root.aircraft_healthbar@color = "1 .874 .0 1"
		} else {
			*node:root.aircraft_healthbar@color = ".0 1 .0 1"
		}
	}

	/**
	 * @note iaircraft interface
	 */
	confunc aircraft_change
	{
		call *node:root.aircraft_checkbuttons
		call *node:root.aircraft_healtbarchange
		*node:root.aircraft_list@lineselected = <1>
	}

	func onWindowOpened
	{
		*cvar:mn_aircrafttab = "aircraft"
		cmd "mn_select_aircraft <cvar:mn_aircraft_id>;"
		cmd "aircraft_update_list;"
	}
}
